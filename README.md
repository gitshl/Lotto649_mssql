# Lotto649_mssql
Simulate Lotto (6 numbers restricted in 1-49) gambling of Taiwan.

Lotto649.sql can be run on [SSMS](https://docs.microsoft.com/zh-tw/sql/ssms/download-sql-server-management-studio-ssms?view=sql-server-2017), in which a database (Lotto649) was created to store all generated Lotto patterns in the table (Lotto649).

## [Lotto649台灣大樂透](http://www.taiwanlottery.com.tw/Lotto649/index.asp)

* A Lotto (樂透一注) consists of 6 numbers (1-49) manually selected or autmoatically generated by computer random functions. Create SNO49 table to store 49 numbers.

  ```sql
  -- table SNO49(ID): generate ID 1 ... 49 corresponding to 49 rows 
  declare @start int = 1, @end int = 49;
  with Sno as (
  select @start as n
  union all
  select n + 1 from Sno where n + 1 <= @end
  )
  select n as ID into SNO49 from Sno
  option (maxrecursion 10000)
  --select * from SNO49
  ```

* Lottery numbers (樂透獎號) contains 6 regular numbers and 1 special number.
* Lotto prizes depend on matched numbers.

## All Lotto Numbers

Six Lotto numbers are unordered, so that "1,2,3,4,5,6" and "6,5,4,3,2,1" denote the same Lotto ticket. Therefore, the total number of unique Lotto649 patterns is: C(49, 6) = 49!/(6!(49-6)!) = 13,983,816. How to use SQL to generate all patterns.

```sql
insert into Lotto649
select A.ID as A, B.ID as B, C.ID as C, D.ID as D, E.ID as E, F.ID as F
from SNO49 A, SNO49 B, SNO49 C, SNO49 D, SNO49 E, SNO49 F
where A.ID < B.ID and B.ID < C.ID and C.ID < D.ID and D.ID < E.ID and E.ID < F.ID;
```

## How fast to search a Lotto

There are about 14 millions Lotto patterns, in which each object contains 10 bytes (1 `int` ID and 6 `tinyint` Numbers). Therefore, the size of Lotto649 table is about `14M * 10B = 140 MB`. Searching a Lotto from the table is fast, if all data are cached in RAM. However, creating index on 6 `tinyint` Numbers provides efficiently retrieving any Lottos while RAM size is limited.

```sql
-- Test response time of seeking a Lotto from 13,983,816 Lotto patterns
select * from Lotto649 where A = 1 and B = 4 and C = 7 and D = 14 and E = 30 and F = 39

-- Create UQ index for rapidly searching and verifying the uniqueness of Lotto patterns 
create UNIQUE INDEX UQ_6No on Lotto649(A, B, C, D, E, F);
go
```

## Shrink Log file

Generating 14M Lotto pattern data in a SQL statement is a single and large transaction so that the log file will be increased more than the size of real data. In this case, the Log file (.ldf) grows to 2.8 GB, although the size of DB (.mdf) is about 270 MB before `UNIQUE index` was created. However, the exploded Log data can not be automatically shrunk for keeping transactional operations. Since the current Log data is surely useless, following code will be applied to shrink the .ldf size to release the disk space.

```sql
-- Truncate the log by changing the database recovery model to SIMPLE.  
ALTER DATABASE Lotto649  
SET RECOVERY SIMPLE;  
GO  
-- Shrink the truncated log file to 10 MB.  
DBCC SHRINKFILE (Lotto649_Log, 10);  
GO  
-- Reset the database recovery model.  
ALTER DATABASE Lotto649  
SET RECOVERY FULL;  
GO 
```

## Stored Procedure for Random Lottos

* `xp_RandomBtw 1, 49, 6, @str out`: Generate 6 different random numbers between 1 and 49, and output as a string containing sorted numbers like '1, 2, 3, 4, 5, 6'.
* [xp_RandomBtw_mssql](https://github.com/gitshl/xp_RandomBtw_mssql) shows the design and issues.

```sql
-- Generate @round random numbers between @n1 and @n2
-- Return string containing numbers separated by ",". 
-- E.g., 1, 2, 3, 4, 5, 6 or 3, 10, 11, 23, 36, 43
create or alter procedure xp_RandomBtw
@n1 int, @n2 int, @round int, @s varchar(100) out
-- Within [@n1, @n2], getting @round different numbers as output string @s
as
	declare @n int, @i int = 1
	declare @tab table(ID int)
	set @s = NULL
	while @i <= @round
	begin 
	select @n = convert(int, rand() * (@n2 - @n1 + 1)) + @n1
	if not exists (select * from @tab where ID = @n)
	begin
	insert @tab values(@n)
	set @i = @i + 1
	end
	end
	select @s = coalesce(@s + N', ', N'') + convert(varchar(10), ID) from @tab order by ID
	return
go

-- Test Insertion of random Lotto into a temporary table
declare @str varchar(100)
exec xp_RandomBtw 1, 49, 6, @str out; -- [1, 49] get 6 different numbers as output string
print @str

-- Test Insertion of 10 random Lottos into a temporary table
create table #Ticket(A tinyint, B tinyint, C tinyint, D tinyint, E tinyint, F tinyint); 
declare @str2 varchar(100), @i int = 0
declare @sql nvarchar(max) = ''
while @i < 10
begin
	exec xp_RandomBtw 1, 49, 6, @str2 out; -- [1, 49] get 6 different numbers as output string
	select @sql = 'insert #Ticket values(' + @str2 + ')'
	print @str2
	EXEC sp_executesql @sql
	set @i = @i + 1
end
select * from #Ticket

-- Test Generate a random Lotto from ID
declare @N int = 13983816
select * from Lotto649 where ID = convert(int, rand() * @N) + 1
```

